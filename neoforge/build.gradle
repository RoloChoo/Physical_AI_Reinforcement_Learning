plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
}

repositories {
    // [수정됨] ODE4J를 다운로드하기 위해 Maven Central이 필요합니다.
    mavenCentral() 
    maven { url = 'https://maven.neoforged.net/releases' }
}

dependencies {
    neoForge "net.neoforged:neoforge:${rootProject.neoforge_version}"
    // Remove the next line if you don't want to depend on the API
    //modApi "dev.architectury:architectury-neoforge:${rootProject.architectury_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionNeoForge")) { transitive = false }

    // [수정됨] ODE4J 라이브러리 추가
    // implementation: 코드 작성 시 IDE에서 인식하기 위함
    // shadowCommon: 빌드된 jar 파일 안에 라이브러리를 포함시키기 위함 (Architectury 설정에 따름)
    implementation 'org.ode4j:ode4j:0.4.2'
    shadowCommon 'org.ode4j:ode4j:0.4.2'
}

processResources {
    inputs.property "version", project.version
    inputs.property "neoforge_version_range", project.neoforge_version_range

    filesMatching("META-INF/neoforge.mods.toml") {
        expand(rootProject.properties)
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"

    // [수정됨] 패키지 재배치 (Relocation)
    // 다른 모드도 ODE4J를 사용할 경우 충돌이 날 수 있으므로, 내 모드 패키지 안으로 숨깁니다.
    // 예: org.ode4j -> com.kAIS.KAIMyEntity.lib.ode4j
    relocate 'org.ode4j', 'com.kAIS.KAIMyEntity.lib.ode4j'
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveVersion.set("NeoForge${project.minecraft_version}-${project.mod_version}")
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

runClient{
	args "--username","Dev"
	args "--width","1920"
	args "--height","1080"
}
